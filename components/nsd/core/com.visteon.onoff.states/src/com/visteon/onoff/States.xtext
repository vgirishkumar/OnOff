grammar com.visteon.onoff.States with org.eclipse.xtext.common.Terminals

generate states "http://www.visteon.com/onoff/States"
import "http://www.visteon.com/onoff/Coom" as coom

NodeStateConfiguration:
	('package' name=FQN)?
	imports+=Import*
	(nsd=NodeStateDiagram)?
	(clientConfig+=ClientConfiguration)*;

Import:
	'import' importURI=STRING;

NodeStateDiagram:
	{NodeStateDiagram}
	'NodeStateDiagram' '{'
	('globalEdgeweight' gEdgeWeight=INT)?
	('globalTransitionTimeout' gTransitionTimeoutInMilliseconds=INT)?
	(nodeStates+=NodeState |
	nodeTransitions+=NodeTransition)*
	'}';

NodeState:
	(initial?='initial')? 'State' name=ID
	('{'
	'}')?;

NodeTransition:
	(essential?='essential')? 'Transition' name=ID ':' fromState=[NodeState] '->' toState=[NodeState]
	('{'
	('edgeweight' edgeweight=INT)?
	('timeout' timeoutInMilliseconds=INT)?
	'}')?;

ClientConfiguration:
	'Client' name=FQN // instance name - scope coming from COOM
	'{'
	('coom' coomRef=[coom::ComponentOnOffManifest|FQN])
	('clientTransitionTimeout' transTimeoutInMilliseconds=INT)?
	(componentTransitions+=ComponentTransition |
	nodeStateAssociations+=NodeStateAssociation |
	componentStates+=ComponentState |
	features+=Feature)*
	'}';

ComponentTransition:
	(essential?='essential')? 'Transition' transition=[coom::Transition|FQN]
	'{'
	('timeout' timeoutInMilliseconds=INT)?
	('max-retries' maxretries=INT)?
	'}';

ComponentState:
	'State' state=[coom::State|FQN]
	'{'
	(stateDependencies=StateDependency)? &
	(featureDependencies=FeatureDependency)? &
	(transitionDependencies=TransitionDependency)?
	'}';

StateDependency:
	'depends on states' states+=[coom::State|FQN] (',' states+=[coom::State|FQN])*;

TransitionDependency:
	'depends on transitions' transistions+=[coom::Transition|FQN] (',' transistions+=[coom::Transition|FQN])*;

FeatureDependency:
	'depends in features' features+=[Feature|FQN] (',' features+=[Feature|FQN])*;

NodeStateAssociation:
	'For' nodeState=[NodeState] 'set' clientState=[coom::State|FQN];

Feature:
	'Feature' name=ID
	'{'
	'states' featureStates+=[coom::State|FQN] (',' featureStates+=[coom::State|FQN])*
	'}';

FQN:
	ID ('.' ID)*;