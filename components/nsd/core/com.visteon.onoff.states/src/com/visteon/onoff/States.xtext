grammar com.visteon.onoff.States with org.eclipse.xtext.common.Terminals

generate states "http://www.visteon.com/onoff/States"
import "http://www.visteon.com/onoff/Coom" as coom

NodeStateConfiguration:
	imports+=Import*
	nsd=NodeStateDiagram
	clientConfig+=ClientConfiguration*;

Import:
	'import' importURI=STRING;

NodeStateDiagram:
	{NodeStateDiagram}
	'NodeStateDiagram' '{'
	('globalEdgeweight' gEdgeWeight=INT)?
	('globalTransitionTimeout' gTransitionTimeoutInMilliseconds=INT)?
	(states+=NodeState |
	transitions+=NodeTransition)*
	'}';

NodeState:
	(initial?='initial')? 'State' name=ID;

NodeTransition:
	(essential?='essential')? 'Transition' name=ID ':' fromState=[NodeState] '->' toState=[NodeState]
	('{'
	('edgeweight' edgeweight=INT)?
	('timeout' timeoutInMilliseconds=INT)?
	'}')?;

ClientConfiguration:
	'Client' name=ID // instance name - scope coming from COOM
	'{'
	('coom' coomRef=[coom::ComponentOnOffManifest])
	('clientTransitionTimeout' transTimeoutInMilliseconds=INT)?
	(transitions+=ComponentTransition |
	states+=ComponentState |
	features+=ComponentFeature | nodeStateAssociations+=NodeStateAssociation)*
	'}';

ComponentTransition:
	(essential?='essential')? 'Transition' transition=[coom::Transition|FQN]
	'{'
	('timeout' timeoutInMilliseconds=INT)?
	('max-retries' maxretries=INT)?
	((stateDependency=StateDependency)? &
	(featureDependency=FeatureDependency)?)
	'}';

ComponentState:
	'State' state=[coom::State|FQN]
	'{'
	((stateDependency=StateDependency)? &
	(featureDependency=FeatureDependency)? &
	(transitionDependency=TransitionDependency)?)
	'}';

StateDependency:
	'depends on states' states+=[coom::State|FQN] (',' states+=[coom::State|FQN])*;

TransitionDependency:
	'depends on transitions' transistions+=[coom::Transition|FQN] (',' transistions+=[coom::Transition|FQN])*;

FeatureDependency:
	'depends on features' features+=[ComponentFeature] (',' features+=[ComponentFeature])*;

NodeStateAssociation:
	'for' clientState=[coom::State|FQN] 'set system-states' states+=[NodeState] (',' states+=[NodeState])*;

ComponentFeature:
	'Feature' name=ID
	'{'
	'states' states+=[coom::State|FQN] (',' states+=[coom::State|FQN])*
	'}';

FQN:
	ID ('.' ID)*; 